import groovy.json.*
import java.nio.file.Path

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'de.undercouch.download' version '3.3.0'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

sourceSets {
    api {
        java {
            srcDir 'src/api/java'
        }
        resources {
            srcDir 'src/api/resources'
        }
        compileClasspath += configurations.getByName("minecraft")
    }
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/generated/resources'
            exclude "assets/structurize/lang/default.json"
        }
        compileClasspath += sourceSets.main.compileClasspath
        compileClasspath += sourceSets.api.output
    }
}

configurations {
    contained {
        transitive = false
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
    apiImplementation "com.intellij:annotations:+@jar"
    implementation 'com.intellij:annotations:+@jar'
    implementation "org.mariuszgromada.math:MathParser.org-mXparser:${project.math_parser_version}"
    contained "org.mariuszgromada.math:MathParser.org-mXparser:${project.math_parser_version}"
    implementation "com.ldtteam:datagenerators:${project.datagenerators_version}"
}

version = project.minecraft_version + System.getenv().containsKey("Version") ? System.getenv("Version") : 'null'
group = project.base_group
archivesBaseName = project.base_mod_id

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: project.mappings_channel, version: project.mappings_version
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('runClient')

            // Recommended logging data for a userdev environment
            // property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            // OPTIFINE GUIDE
            // find and download/compile latest OptiFineDevTweaker, classifier "all"
            // put this and (unpacked) optifine into mods in runClient folder
            // alternate or comment out (if you have default gradle cache location) next property to target "caches/forge_gradle/minecraft_repo/versions/{mcVersion}/client.jar"
            // property 'ofdev.mcjar', 'P:\\apps\\common\\gradle-cache\\caches\\forge_gradle\\minecraft_repo\\versions\\1.16.1\\client.jar'

            mods {
                structurize {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        server {
            workingDirectory project.file('runServer')

            // Recommended logging data for a userdev environment
            // property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            mods {
                structurize {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        data {
            workingDirectory project.file('runClient')

            // Recommended logging data for a userdev environment
            // property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'info'

            args '--mod', project.base_mod_id, '--all', '--output', file('src/main/generated/resources'), '--input', file('src/main/resources'), '--existing', file('src/main/resources/')

            mods {
                structurize {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }
    }
}

/* TODO: possible only with newer gradle, now lang changes are NOT working for eclipse and vscode
eclipse {
    autoBuildTasks mergeLangJson(with proper file argument)
}

// this doesnt work as it's overriden every eclipse build call
eclipse.classpath.file.beforeMerged {
    entries.each {
        if (it.path.equals('src/main/resources'))
        {
            project.mergeLangJson(file(it.output).toPath())
        }
    }
}
*/

//From forge
task downloadCrowdin() {
    ext {
        output = file('build/crowdin_raw.zip')
        update = file('build/crowdin.json')
        id = 'structurize'
    }
    onlyIf {
        System.getenv().containsKey("crowdinKey") && !project.gradle.startParameter.isOffline()
    }
    doLast {
        download {
            src "https://api.crowdin.com/api/project/${id}/export?key=${System.getenv("crowdinKey")}&json"
            dest update
            overwrite true
        }
        if (!update.text.contains('success')) {
            throw new RuntimeException("Crowdin export failed, see ${update} for more info")
        }
        download {
            src "https://api.crowdin.com/api/project/${id}/download/all.zip?key=${System.getenv("crowdinKey")}"
            dest output
            overwrite true
        }
    }
}

task crowdin(type: Zip) {
    dependsOn downloadCrowdin
    onlyIf {
        !downloadCrowdin.state.skipped
    }
    baseName = project.name
    version = project.version
    classifier = 'crowdin'
    destinationDir = file('build/distributions')
    from(zipTree(downloadCrowdin.output)){
        eachFile {
            if (!it.path.startsWith('assets/')) {
                it.relativePath = new RelativePath(true, it.relativePath.segments.drop(4))
            }
        }
        exclude { it.isDirectory() }
        rename { it.toLowerCase() }
    }
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier 'api'
}

javadoc {
    source += sourceSets.api.allSource
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
    from sourceSets.api.allJava
}

idea {
    module {
        inheritOutputDirs = true
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    from sourceSets.api.output
    from sourceSets.main.output

    // add crowdin locales
    from { !crowdin.state.skipped ? zipTree(crowdin.archivePath) : null}
    dependsOn crowdin

    classifier ''

    manifest {
        attributes([
            'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
            "Specification-Title": project.base_mod_id,
            "Specification-Vendor": "ldtteam",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${project.version}",
            "Implementation-Vendor" :"ldtteam",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


shadowJar {
    from sourceSets.main.output
    from sourceSets.api.output

    configurations = [project.configurations.contained]
    // Relocate apache commons-math3 to prevent conflicts with other mods that include it
    relocate 'org.mariuszgromada', 'com.ldtteam.shaded.mariuszgromada'

    classifier = 'universal' // Replace the default JAR
}

reobf {
    jar {
    }
    shadowJar {
    }
}

artifacts {
    shadowJar
    jar
    packageJavadoc
    sourceJar
    apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from project.components.java

            artifact shadowJar
            artifact packageJavadoc
            artifact sourceJar
            artifact apiJar

            pom {
                url = 'https://github.com/ldtteam/Structurize'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'OrionDevelopment'
                        name = 'Marc Hermans'
                        email = 'marc.hermans@ldtteam.com'
                    }
                    developer {
                        id = 'Raycoms'
                        name= 'Ray Neiheiser'
                        email = 'ray.neiheiser@ldtteam.com'
                    }
                }
                contributors {
                    contributor {
                        id = 'ldtteam'
                        name = 'Let\'s Develop Together - Team'
                        email = 'info@ldtteam.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ldtteam/structurize.git'
                    developerConnection = 'scm:git:ssh://github.com/ldtteam/structurize.git'
                    url = 'https://github.com/ldtteam/Structurize'
                }
            }

            //Replace all deobf dependencies with normal jar references.
            pom.withXml { xmlProvider ->
                NodeList dependencies = asNode().dependencies
                NodeList allDeps = dependencies.'*';

                // Remove forge deps
                allDeps.findAll() { Node el ->
                    el.artifactId.text() == 'forge' && el.groupId.text() == 'net.minecraftforge'
                }.forEach() { Node el ->
                    el.parent().remove(el)
                }
                //remove forgegradle's mapped suffix from versions & set as optional so anyone else doesn't inherit them
                allDeps.findAll() { Node el ->
                    el.version.text().contains('_mapped_')
                }.each { Node el ->
                    NodeList version = el.version
                    version.each {
                        it.setValue(it.text().substring(0, it.text().indexOf('_mapped_')))
                    }
                }
            }
        }
    }

    if (System.getenv().containsKey("LDTTeamJfrogUsername") && System.getenv().containsKey("LDTTeamJfrogPassword")) {
        project.logger.log(LogLevel.WARN, "Uploading to JFrog enabled.")
        repositories {
            maven {
                name 'LDTTeamJfrog'
                credentials {
                    username System.getenv().get("LDTTeamJfrogUsername")
                    password System.getenv().get("LDTTeamJfrogPassword")
                }
                url 'https://ldtteam.jfrog.io/ldtteam/mods-maven'
            }
        }
    }
    else {
        project.logger.log(LogLevel.WARN, "Uploading to JFrog disabeled.")
    }
}

task setupDecompWorkspace {
    afterEvaluate {
        println "Setup"
    }
}

task setupCIWorkspace {
    afterEvaluate {
        println "Setup"
    }
}

processResources {
    doLast {
        mergeLangJson(getDestinationDir().toPath())
    }
}

def mergeLangJson(Path destDir) {
    if (file('src/main/generated/resources/assets/structurize/lang/default.json').exists()) {
        def slurper = new JsonSlurper()
        def standard = slurper.parse(file('src/main/resources/assets/structurize/lang/en_us.json'))
        def generated = slurper.parse(file('src/main/generated/resources/assets/structurize/lang/default.json'))

        standard += generated

        println destDir.resolve('assets/structurize/lang/en_us.json')
        file(destDir.resolve('assets/structurize/lang/en_us.json')).write(JsonOutput.toJson(standard))
    }
}

curseforge {
    if (System.getenv().CURSEAPIKEY != null && System.getenv().CURSERELEASETYPE != null)
    {
        apiKey = System.getenv().CURSEAPIKEY

        project {
            id = project.curse_id
            addGameVersion project.minecraft_version
            changelog = file('build/changelog.md')
            changelogType = 'markdown'
            releaseType = System.getenv().CURSERELEASETYPE

            mainArtifact shadowJar
            addArtifact jar
            addArtifact apiJar
        }
    }
    else
    {
        logger.lifecycle("Cannot run the CurseUpload sequence. No API-Key or release type has been provided.")
    }
}

task("createChangelog") {
    group = 'upload'

    doLast {
        def teamCityURL = "https://buildsystem.ldtteam.com/"
        def file = new FileOutputStream("build/changelog.md")
        def out = new BufferedOutputStream(file)
        def changesXML = new XmlSlurper().parse(teamCityURL + "guestAuth/app/rest/changes?locator=build:(id:" + teamcity["teamcity.build.id"] + ")")

        def changes = changesXML.change
        println("createChangelog: Identified " + changes.size() + " changes to be written into the changelog.")

        out << "# Minecolonies Changelog \n"
        out << "## Version: _" + version + "_ \n"

        if (changes.size() > 0) {
            for (int i = 0; i < changes.size(); i++) {
                def changeDetailsURL = teamCityURL + "guestAuth/app/rest/changes/id:" + changes[i].@id.text()
                def changeDetailsXml = new XmlSlurper().parse(changeDetailsURL)
                def changeComment = changeDetailsXml.comment.text().trim()

                out << "* " + changeComment + "\n"
            }
        } else {
            out << "No Changes detected!"
        }

        out.close()
    }
}
